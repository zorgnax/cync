#!/usr/bin/perl
use Getopt::Long;
use Data::Dumper;
use strict;
use warnings;

my ($noact, $src, $dest, @find_args);
parse_options() or usage();
sync_all();

sub usage {
    print "Usage: $0 [-n] <src> <dest> [find-args]\n";
    exit;
}

sub parse_options {
    GetOptions(
        noact => \$noact,
        '<>' => sub {$src = parse_ref($_[0]); die "!FINISH"})
        or return;
    $dest = parse_ref(shift @ARGV);
    return if !$src || !$dest;
    @find_args = @ARGV;
    return 1;
}

sub parse_ref {
    my ($ref) = @_;
    return if !$ref;
    $ref =~ s{/*$}{}g;
    if ($ref =~ /([^:]+):(.*)/) {
        return {host => $1, file => $2, ref => $ref};
    }
    return {host => "", file => $ref, ref => $ref};
}

sub sync_all {
    my $src_files = find($src);
    my $dest_files = find($dest);
    my %processed;
    for my $name (sort keys %$src_files, keys %$dest_files) {
        next if $processed{$name}++;
        sync($name, $src_files->{$name}, $dest_files->{$name});
    }
    print STDERR "done!\n";
}

sub find {
    my ($ref) = @_;
    my %files;
    my @cmd = ("find", $ref->{file}, @find_args);
    unshift @cmd, "ssh", $ref->{host} if $ref->{host};
    print STDERR "@cmd\n";
    push @cmd, "-printf", "%y %Ts %m \\0%p\\0 \\0%l\\0\\n";
    open my $fh, "-|", @cmd;
    while (<$fh>) {
        my ($type, $mtime, $mode, $name, $target) =
            /(.) (\d+) (\d+) \0(.+)\0 \0(.*)\0/ or next;
        $name =~ s{^\Q$ref->{file}\E?}{};
        $files{$name} = {
            type => $type, mtime => $mtime, mode => $mode, target => $target,
        };
    }
    close $fh;
    return \%files;
}

sub mysystem {
    my ($ref, @cmd) = @_;
    unshift @cmd, "ssh", $ref->{host} if $ref && $ref->{host};
    print STDERR "@cmd\n";
    return if $noact;
    system @cmd;
    exit($?>>8) if $?>>8;
}

sub sync {
    my ($name, $a, $b) = @_;
    if ($b && (!$a || $a->{type} ne $b->{type})) {
        mysystem $dest, "rm", "-r", "$dest->{file}$name";
    }
    return if !$a;
    my $synced;
    if ($a->{type} eq "f" && (!$b || $b->{mtime} ne $a->{mtime})) {
        my $cp = $src->{host} || $dest->{host} ? "scp" : "cp";
        mysystem undef, $cp, "$src->{ref}$name", "$dest->{ref}$name";
        $synced = 1;
    }
    elsif ($a->{type} eq "l" && (!$b || $b->{target} ne $a->{target})) {
        mysystem $dest, "ln", "-s", $a->{target}, "$dest->{file}$name";
        return;
    }
    elsif ($a->{type} eq "d" && !$b) {
        mysystem $dest, "mkdir", "$dest->{file}$name";
        $synced = 1;
    }
    if ($synced || !$b || $b->{mode} ne $a->{mode}) {
        mysystem $dest, "chmod", $a->{mode}, "$dest->{file}$name";
        $synced = 1;
    }
    if ($synced) {
        mysystem $dest, "touch", "-d", "\@$a->{mtime}", "$dest->{file}$name";
    }
}

